; depends: strings_programs.plf

(declare string_length_non_empty (! s term
                                 (! t term
                                 (! p (holds (not (= s t)))
                                 (! r (^ (sc_string_is_empty t) tt)
                                    (holds (not (= (str.len s) (int 0)))))))))

; Computes the conclusion of the PfRule::CONCAT_EQ rule
(function sc_concat_eq ((s term) (t term) (rev flag) (u sort)) term
  (match (sc_strip_prefix
           (sc_string_to_flat_form s rev u) 
           (sc_string_to_flat_form t rev u) u)
    ((pair ss ts)
      (= 
        (sc_string_from_flat_form ss rev u)
        (sc_string_from_flat_form ts rev u)))))

(declare concat_eq (! s term 
                   (! t term 
                   (! res term 
                   (! rev flag
                   (! u sort
                   (! p (holds (= s t))
                   (! r (^ (sc_concat_eq s t rev u) res) 
                      (holds res)))))))))

; Helper method for PfRule::CONCAT_UNIFY. Returns ok if s or a prefix of its
; head is equal to s1, where the (suffix of the) head of the reverse of s is
; checked if rev is tt. Fails otherwise.
(function sc_concat_head_or_self ((s term) (s1 term) (rev flag) (u sort)) Ok
  (ifequal s s1
    ok
    (let ss (sc_string_rev s rev u)
    (let sh (sc_string_head ss)
    (ifequal sh s1
      ok
      ; otherwise we may be splitting a constant, get s1 in n-ary form
      (let s1n (sc_string_rev (sc_string_nary_intro s1 u) rev u)
      ; it may be that s itself is a constant, in which case we must consider
      ; it as a whole, not just its head
      (ifequal (sc_string_is_prefix s1n ss u) tt
        ok
        ; we are splitting a constant in the head of s1, notice we must reverse
        ; its characters, since they were not reversed when computing ss
        (ifequal (sc_string_is_prefix s1n (sc_string_rev sh rev u) u) tt
          ok
          (fail Ok))))))))
)

; Computes whether the heads of the premise (= s t) match s1 and t1 for the
; PfRule::CONCAT_UNIFY rule
(function sc_concat_unify ((s term) (t term) (s1 term) (t1 term) (rev flag) (u sort)) Ok
  (ifequal (sc_concat_head_or_self s s1 rev u) ok
    (ifequal (sc_concat_head_or_self t t1 rev u) ok
      ok
      (fail Ok))
    (fail Ok)))

(declare concat_unify (! s term
                      (! t term
                      (! s1 term
                      (! t1 term
                      (! rev flag
                      (! u sort
                      (! p (holds (= s t))
                      (! p1 (holds (= (str.len s1) (str.len t1)))
                      (! r (^ (sc_concat_unify s t s1 t1 rev u) ok)
                          (holds (= s1 t1))))))))))))

; Computes the conclusion of the PfRule::CONCAT_CSPLIT rule
(function sc_concat_csplit ((thead term) (t term) (s term) (rev flag) (u sort)) term
  (match (sc_string_to_flat_form t rev u)
    ((apply t1 t2)
      (ifequal (getarg f_str.++ t1) thead
        (match (sc_string_to_flat_form s rev u)
          ((apply s1 s2)
            (let s12 (getarg f_str.++ s1)
            (match s12
              ((char n)
                (= thead
                (ifequal rev ff
                  (str.++ s12 (str.++ (skolem (sc_skolem_suffix_rem thead (int 1))) emptystr))
                  (str.++ (skolem (sc_skolem_prefix thead (a.- (str.len thead) (int 1)))) (str.++ s12 emptystr)))))))))
          (fail term))))
)

(declare concat_csplit 
  (! t1 term
  (! t term
  (! s term
  (! res term
  (! rev flag
  (! u sort
  (! p1 (holds (= t s))
  (! p2 (holds (not (= (str.len t1) (int 0))))
  (! r (^ (sc_concat_csplit t1 t s rev u) res)
    (holds res)))))))))))

; Checks whether the conditions on the premise (= s t) is satisfied for the
; PfRule::CONCAT_CONFLICT rule
(function sc_concat_conflict ((s term) (t term) (rev flag) (u sort)) Ok
  (match (sc_strip_prefix
           (sc_string_to_flat_form s rev u)
           (sc_string_to_flat_form t rev u) u)
    ((pair ss ts)
      (ifequal
        (sc_string_first_char_or_empty ss u)
        (sc_string_first_char_or_empty ts u)
        (fail Ok)
        ok))))

(declare concat_conflict (! s term
                         (! t term
                         (! rev flag
                         (! u sort
                         (! p (holds (= s t))
                         (! r (^ (sc_concat_conflict s t rev u) ok)
                            (holds false))))))))

; For sequences, we use a variant of the PfRule::CONCAT_CONFLICT rule where the
; justification for the clashing characters is given in the form of a
; disequality. This is due to the fact that check whether a term is value is
; not a simple syntactic check for some types.
(function sc_concat_conflict_deq ((s term) (t term) (sc term) (tc term) (rev flag) (u sort)) Ok
  (match (sc_strip_prefix
           (sc_string_to_flat_form s rev u)
           (sc_string_to_flat_form t rev u) u)
    ((pair ss ts)
      (ifequal (sc_string_first_char_or_empty s u) sc
        (ifequal (sc_string_first_char_or_empty t u) tc
          ok
          (fail Ok))
        (fail Ok)))))

(declare concat_conflict_deq (! s term
                             (! t term
                             (! sc term
                             (! tc term
                             (! rev flag
                             (! u sort
                             (! p (holds (= s t))
                             (! pc (holds (not (= sc tc)))
                             (! r (^ (sc_concat_conflict_deq s t sc tc rev u) ok)
                                (holds false)))))))))))

(function sc_string_length_pos ((t term) (u sort)) term
  (or (and (= (str.len t) (int 0)) (and (= t (sc_mk_emptystr u)) true)) (or (a.> (str.len t) (int 0)) false))
)

(declare string_length_pos (! t term
                           (! u sort
                           (! s term
                           (! r (^ (sc_string_length_pos t u) s)
                              (holds s))))))

(declare re_inter (! x term (! s term (! t term (! p1 (holds (str.in_re x s)) (! p2 (holds (str.in_re x t)) 
  (holds (str.in_re x (re.inter s (re.inter t re.all))))))))))

(declare string_reduction (! r term (! t term (! s sort (! u (^ (sc_string_reduction t s) r)
  (holds r))))))))

(declare string_eager_reduction (! r term (! t term (! s sort (! u (^ (sc_string_eager_reduction t s) r)
  (holds r))))))

; Computes the conclusion of PfRule::RE_UNFOLD_POS
(function sc_re_unfold_pos ((t term) (r term)) term
  (match r
    ((apply r1 r2)
      (match r1
        ; case for star
        (f_re.*
          (let rr (re.++ r2 (re.++ r (re.++ r2 re.empty)))
          (match (sc_re_unfold_pos_concat t rr rr 0)
            ((pair p1 p2)
              (or (= t emptystr)
                (or (str.in_re t r2)
                  (or (and (sc_string_nary_elim (and (= t p1) p2) String) (sc_non_empty_concats p1 String))
                    false)))))))
        ((apply r11 r12)
          (match r11
            ; case for concatenation
            (f_re.++
              (match (sc_re_unfold_pos_concat t r r 0)
                ((pair p1 p2) (sc_string_nary_elim (and (= t p1) p2) String))))
))))))

(declare re_unfold_pos (! t term (! r term (! s term (! f (holds (str.in_re t r)) (! u (^ (sc_re_unfold_pos t r) s) (holds s)))))))
