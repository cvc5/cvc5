(include "../programs/Utils.eo")

; disclaimer: >
;   This function is a builtin function symbol of SMT-LIB denoting
;   universal quantification. We represent it as an ordinary function taking a
;   list as the first child to model cvc5's treatment of quantified formulas.
(declare-const forall (-> @List Bool Bool) :binder @list)

; disclaimer: >
;   This function is a builtin function symbol of SMT-LIB denoting
;   existential quantification. We represent it as an ordinary function taking a
;   list as the first child to model cvc5's treatment of quantified formulas.
(declare-const exists (-> @List Bool Bool) :binder @list)

; returns the type of the element in the list, if the list is a singleton.

; program: $get_witness_type
; args:
; - L List: The list of variables of the witness to process.
; return: >
;   The type of a witness having the singleton list of variables L.
(program $get_witness_type ((x @List))
  (@List) Type
  (
    (($get_witness_type (@list x)) (eo::typeof x))
  )
)

; disclaimer: This function is not in SMT-LIB.
(declare-const witness (-> (! @List :var L) Bool ($get_witness_type L)) :binder @list)

; program: $get_qskolem_type
; args:
; - F Bool: The quantified formula.
; - i Int: The integer indicating the index of the variable.
; return: >
;   The type of the @quantifiers_skolemize term for F and i.
(program $get_qskolem_type ((xs @List) (G Bool) (i Int))
  (Bool Int) Type
  (
    (($get_qskolem_type (forall xs G) i) (eo::typeof (eo::list_nth @list xs i)))
  )
)
; skolems
(declare-const @quantifiers_skolemize (-> (! Type :var T :implicit) (! Bool :opaque :var F) (! Int :opaque :var i) ($get_qskolem_type F i)))
