(include "../programs/Utils.eo")

(include "../theories/Arith.eo")

; disclaimer: >
;   This function is a builtin function symbol of SMT-LIB denoting
;   universal quantification. We represent it as an ordinary function taking a
;   list as the first child to model cvc5's treatment of quantified formulas.
(declare-const forall (-> @List Bool Bool) :binder @list)

; disclaimer: >
;   This function is a builtin function symbol of SMT-LIB denoting
;   existential quantification. We represent it as an ordinary function taking a
;   list as the first child to model cvc5's treatment of quantified formulas.
(declare-const exists (-> @List Bool Bool) :binder @list)

; program: $get_var_at_index
; args:
; - F Bool: The quantified formula.
; - i Int: The integer indicating the index of the variable.
; return: >
;   The i^th variable in the binder of F.
(program $get_var_at_index ((Q (-> @List Bool)) (xs @List) (G Bool) (i Int))
  (Bool Int) Type
  (
    (($get_var_at_index (Q xs G) i) (eo::list_nth @list xs i))
  )
)

; skolems
(declare-const @quantifiers_skolemize (-> (! Type :var T :implicit) (! Bool :opaque :var F) (! Int :opaque :var i) (eo::typeof ($get_var_at_index F i))))

(declare-const @witness_inv_condition (-> (! Bool :opaque :var F) (eo::typeof ($get_var_at_index F 0))))
