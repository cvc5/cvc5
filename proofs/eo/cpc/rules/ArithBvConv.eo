(include "../theories/ArithBvConv.eo")

(include "../programs/Arith.eo")
(include "../programs/Utils.eo")

;;;;; ProofRewriteRule::BV_TO_NAT_ELIM

; program: $abconv_int_to_bv_elim
; args:
; - b (BitVec n): A bitvector.
; - i Int: The current position in b we are considering.
; - w Int: The bitwidth of b.
; - p Int: Equal to 2 to the power of i.
; return: >
;   The result of the reduction for the remaining bits for (bv2nat b),
;   which is an arithmetic sum.
(program $abconv_bv_to_nat_elim ((n Int) (b (BitVec n)) (i Int) (w Int) (p Int))
  ((BitVec n) Int Int Int) Int
  (
    (($abconv_bv_to_nat_elim b w w p) 0)
    (($abconv_bv_to_nat_elim b i w p) (eo::cons +
                                        (ite (= (extract i i b) #b1) p 0)
                                        ($abconv_bv_to_nat_elim b (eo::add i 1) w (eo::mul p 2))))
  )
)

; rule: bv-to-nat-elim
; implements: ProofRewriteRule::BV_TO_NAT_ELIM
; args:
; - eq Bool: The equality between a bv2nat term and an integer term.
; requires: >
;   Reducing the left hand side to an integer term results in the right hand side.
; conclusion: The given equality.
(declare-rule bv-to-nat-elim ((n Int) (b (BitVec n)) (m Int))
  :args ((= (bv2nat b) m))
  :requires (((eo::define ((w ($bv_bitwidth (eo::typeof b))))
                ($singleton_elim ($abconv_bv_to_nat_elim b 0 w 1))) m))
  :conclusion (= (bv2nat b) m)
)

;;;;; ProofRewriteRule::INT_TO_BV_ELIM

; program: $abconv_int_to_bv_elim
; args:
; - n Int: An integer.
; - w Int: The target bitwidth we are converting to.
; - p Int: Equal to 2 to the power of w.
; return: >
;   The result of reducing (int2bv w n), which is a bitvector concatenation.
(program $abconv_int_to_bv_elim ((n Int) (w Int) (p Int))
  (Int Int Int) (BitVec n)
  (
    (($abconv_int_to_bv_elim n 0 p) @bv_empty)
    (($abconv_int_to_bv_elim n w p) (eo::define ((wm1 (eo::add w -1)))
                                    (eo::define ((pd2 (eo::zdiv p 2)))
                                    (eo::cons concat
                                      (ite (>= (mod_total n p) pd2) #b1 #b0)
                                      ($abconv_int_to_bv_elim n wm1 pd2)))))
  )
)

; rule: int-to-bv-elim
; implements: ProofRewriteRule::INT_TO_BV_ELIM
; args:
; - eq Bool: The equality between a bv2nat term and an integer term.
; requires: >
;   Reducing the left hand side to an integer term results in the right hand side.
; conclusion: The given equality.
(declare-rule int-to-bv-elim ((w Int) (n Int) (b (BitVec n)))
  :args ((= (int2bv w n) b))
  :requires ((($singleton_elim ($abconv_int_to_bv_elim n w ($arith_eval_int_pow_2 w))) b))
  :conclusion (= (int2bv w n) b)
)
