(include "../theories/ArithBvConv.eo")

(include "../programs/Arith.eo")
(include "../programs/Utils.eo")

;;;;; ProofRewriteRule::UBV_TO_INT_ELIM

; program: $abconv_int_to_bv_elim
; args:
; - b (BitVec n): A bitvector.
; - i Int: The current position in b we are considering.
; - w Int: The bitwidth of b.
; - p Int: Equal to 2 to the power of i.
; return: >
;   The result of the reduction for the remaining bits for (ubv_to_int b),
;   which is an arithmetic sum.
(program $abconv_ubv_to_int_elim ((n Int) (b (BitVec n)) (i Int) (w Int) (p Int))
  :signature ((BitVec n) Int Int Int) Int
  (
    (($abconv_ubv_to_int_elim b w w p) 0)
    (($abconv_ubv_to_int_elim b i w p) (eo::cons +
                                        (ite (= (extract i i b) #b1) p 0)
                                        ($abconv_ubv_to_int_elim b (eo::add i 1) w (eo::mul p 2))))
  )
)

; rule: ubv-to-int-elim
; implements: ProofRewriteRule::UBV_TO_INT_ELIM
; args:
; - eq Bool: The equality between a ubv_to_int term and an integer term.
; requires: >
;   Reducing the left hand side to an integer term results in the right hand side.
; conclusion: The given equality.
(declare-rule ubv-to-int-elim ((n Int) (b (BitVec n)) (m Int))
  :args ((= (ubv_to_int b) m))
  :requires (((eo::define ((w ($bv_bitwidth (eo::typeof b))))
                (eo::list_singleton_elim + ($abconv_ubv_to_int_elim b 0 w 1))) m))
  :conclusion (= (ubv_to_int b) m)
)

;;;;; ProofRewriteRule::INT_TO_BV_ELIM

; program: $abconv_int_to_bv_elim
; args:
; - n Int: An integer.
; - w Int: The target bitwidth we are converting to.
; - p Int: Equal to 2 to the power of w.
; return: >
;   The result of reducing (int2bv w n), which is a bitvector concatenation.
(program $abconv_int_to_bv_elim ((n Int) (w Int) (p Int))
  :signature (Int (eo::quote w) Int) (BitVec w)
  (
    (($abconv_int_to_bv_elim n 0 p) @bv_empty)
    (($abconv_int_to_bv_elim n w p) (eo::define ((wm1 (eo::add w -1)))
                                    (eo::define ((pd2 (eo::zdiv p 2)))
                                    (eo::cons concat
                                      (ite (>= (mod_total n p) pd2) #b1 #b0)
                                      ($abconv_int_to_bv_elim n wm1 pd2)))))
  )
)

; rule: int-to-bv-elim
; implements: ProofRewriteRule::INT_TO_BV_ELIM
; args:
; - eq Bool: The equality between a bv2nat term and an integer term.
; requires: >
;   Reducing the left hand side to an integer term results in the right hand side.
; conclusion: The given equality.
(declare-rule int-to-bv-elim ((w Int) (n Int) (b (BitVec w)))
  :args ((= (int2bv w n) b))
  :requires (((eo::list_singleton_elim concat ($abconv_int_to_bv_elim n w ($arith_eval_int_pow_2 w))) b))
  :conclusion (= (int2bv w n) b)
)
