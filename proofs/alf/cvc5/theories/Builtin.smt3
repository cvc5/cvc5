(include "../programs/Utils.smt3")

; Note that the Bool sort is native to ALF

(declare-const ite (-> (! Type :var A :implicit) Bool A A A))
(declare-const not (-> Bool Bool))

(declare-const or (-> Bool Bool Bool)
   :right-assoc-nil false
)
(declare-const and (-> Bool Bool Bool)
   :right-assoc-nil true
)
(declare-const => (-> Bool Bool Bool)
   :right-assoc
)
(declare-const xor (-> Bool Bool Bool)
   :left-assoc
)

(declare-const = (-> (! Type :var A :implicit) A A Bool)
   :chainable and
)

(program get_lambda_type ((x @List) (xs @List :list) (B Type))
  (@List Type) Type
  (
    ((get_lambda_type (@list x xs) B) (-> (alf.typeof x) (get_lambda_type xs B)))
    ((get_lambda_type @list.nil B)    B)
  )
)
(declare-const lambda (-> (! Type :var B :implicit) (! @List :var L) B (get_lambda_type L B)) :binder @list)

(declare-const distinct (-> (! Type :var A :implicit) A A Bool) :pairwise and)

; generic variables
; NOTE: does not check that U is a numeral
(declare-const const (-> (! Type :var U :implicit) U (! Type :var T) T))

(declare-const skolem (-> (! Type :var A :implicit) A A))

; We construct all skolems by wrapping them in an application of `skolem`.
; The argument is either
; (1) an application of an internal function symbol @ID where ID is the
; skolem function id,
; (2) a term t, in the case of purify skolems, where t is the term.

; For example, the array diff skolem for arrays A and B is:
;   (skolem (@array_deq_diff A B))
; where we have:
;   (declare-const @array_deq_diff
;      (-> (! Type :var T :implicit) (! Type :var U :implicit) (Array T U) (Array T U) T))

