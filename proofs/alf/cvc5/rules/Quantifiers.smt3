(include "../programs/Quantifiers.smt3")
(include "../theories/Quantifiers.smt3")


; @rule instantiate
; Implements ProofRule::INSTANTIATE.
(declare-rule instantiate ((F Bool) (xs @List) (ts @List))
  :premises ((forall xs F))
  :args (ts)
  :conclusion ($substitute_list xs ts F))

; @program $mk_skolems
; @param arg1 : @List. The bound variable list to process.
; @param arg2 : Bool. The body of the quantified formula. This impacts the
; definition of the introduced skolems.
; @return ret : @List. The list of skolem variables for the quantified formula
; whose bound variable list is arg1 and whose body is arg2.
(program $mk_skolems ((x @List) (xs @List :list) (F Bool))
  (@List Bool) @List
  (
    (($mk_skolems (@list x xs) F) (alf.cons @list (@quantifiers_skolemize F x) ($mk_skolems xs F)))
    (($mk_skolems @list.nil F)    @list.nil)
  )
)

; @rule skolemize
; Implements ProofRule::SKOLEMIZE.
(declare-rule skolemize ((F Bool))
  :premises (F)
  :conclusion
    (alf.match ((T Type) (x @List) (G Bool))
        F
        (
          ((exists x G)       ($substitute_list x ($mk_skolems x F) G))
          ((not (forall x G)) ($substitute_list x ($mk_skolems x (exists x (not G))) (not G)))
        ))
)

; @rule skolem_intro
; Implements ProofRule::SKOLEM_INTRO.
(declare-rule skolem_intro ((T Type) (t T))
  :args (t)
  :conclusion 
    (alf.match ((T Type) (x T) (F Bool))
      t
      (
        ; special case for witness
        ((@quantifiers_skolemize (exists (@list x) F) x) (= t (witness (@list x) F)))
        ((@purify x)                                     (= t x))
      )
   )
)

; @rule alpha_equiv
; Implements ProofRule::ALPHA_EQUIV.
; B is the formula to apply to
; vs is a list of variables to substitution for the variable list ts
; Note we don't currently check whether this is valid renaming.
(declare-rule alpha_equiv ((B Bool) (vs @List) (ts @List))
  :args (B vs ts)
  :conclusion (= B ($substitute_list vs ts B))
)
