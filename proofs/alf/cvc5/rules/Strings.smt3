(include "../theories/Strings.smt3")
(include "../theories/Quantifiers.smt3")
(include "../programs/Strings.smt3")

;;-------------------- Core

(declare-rule string_length_pos ((U Type) (s U))
  :args (s)
  :conclusion
    (or (and (= (str.len s) 0) (= s (mk_emptystr (alf.typeof s)))) (> (str.len s) 0))
)

(declare-rule string_length_non_empty ((U Type) (s Type) (t Type))
  :premises ((not (= s t)))
  :requires ((($str_is_empty t) true))
  :conclusion (not (= (str.len s) 0))
)

; the ProofRule::CONCAT_EQ rule
(declare-rule concat_eq ((U Type) (s U) (t U) (rev Bool))
  :premises ((= s t))
  :args (rev)
  :conclusion
    (alf.match ((ss U) (ts U))
      (strip_prefix
           (string_to_flat_form s rev)
           (string_to_flat_form t rev))
      (((@pair ss ts)
        (= 
          (string_from_flat_form ss rev)
          (string_from_flat_form ts rev)))))
)

; returns true if s1 is a prefix of s, taking into account flattening
(define string_is_prefix ((U Type :implicit) (s U) (s1 U) (rev Bool))
  (alf.define ((sf (string_to_flat_form s rev)))
  (alf.define ((sf1 (string_to_flat_form s1 rev)))
  (alf.define ((U (alf.typeof s)))
  (nary.is_prefix str.++ (mk_emptystr U) sf1 sf))))
)

; ProofRule::CONCAT_UNIFY
(declare-rule concat_unify ((U Type) (s U) (t U) (s1 U) (t1 U) (rev Bool))
  :premises ((= s t) (= (str.len s1) (str.len t1)))
  :args (rev)
  :requires (((string_is_prefix s s1 rev) true) ((string_is_prefix t t1 rev) true))
  :conclusion (= s1 t1)
)

; ProofRule::CONCAT_CSPLIT
(declare-rule concat_csplit ((U Type) (t (Seq U)) (s (Seq U)) (u (Seq U)) (rev Bool))
  :premises ((= t s) (not (= (str.len u) 0)))
  :args (rev)
  :conclusion
    (alf.match ((t1 (Seq U)) (t2 (Seq U) :list))
      (string_to_flat_form t rev)
      (((str.++ t1 t2)
        (alf.requires t1 u
          (alf.match ((s1 (Seq U)) (s2 (Seq U) :list))
            (string_to_flat_form s rev)
            (((str.++ s1 s2)
              (alf.requires (string_check_length_one s1) true    ; checks if char
                (= u
                  (alf.ite rev
                    (str.++ (@purify (skolem_prefix u (- (str.len u) 1))) s1)
                    (str.++ s1 (@purify (skolem_suffix_rem u 1)))))))))))))
)

; ProofRule::CONCAT_SPLIT
(declare-rule concat_split ((U Type) (t (Seq U)) (s (Seq U)) (tc (Seq U)) (sc (Seq U)) (rev Bool))
  :premises ((= t s) (not (= (str.len tc) (str.len sc))))
  :args (rev)
  :conclusion
    (alf.match ((t1 (Seq U)) (t2 (Seq U) :list))
      (string_to_flat_form t rev)
      (((str.++ t1 t2)
        (alf.requires t1 tc
          (alf.match ((s1 (Seq U)) (s2 (Seq U) :list))
            (string_to_flat_form s rev)
            (((str.++ s1 s2)
              (alf.requires s1 sc
                (alf.define ((k (@purify (skolem_unify_split t1 s1 rev))))
                (and 
                  (or 
                    (= t1
                      (alf.ite rev
                        (str.++ k s1)
                        (str.++ s1 k)))
                    (= s1
                      (alf.ite rev
                        (str.++ k t1)
                        (str.++ t1 k))))
                    (not (= k (mk_emptystr (alf.typeof k))))
                    (> (str.len k) 0)
                ))))))))))
)

; ProofRule::CONCAT_LPROP
(declare-rule concat_lprop ((U Type) (t (Seq U)) (s (Seq U)) (tc (Seq U)) (sc (Seq U)) (rev Bool))
  :premises ((= t s) (> (str.len tc) (str.len sc)))
  :args (rev)
  :conclusion
    (alf.match ((t1 (Seq U)) (t2 (Seq U) :list))
      (string_to_flat_form t rev)
      (((str.++ t1 t2)
        (alf.requires t1 tc
          (alf.match ((s1 (Seq U)) (s2 (Seq U) :list))
            (string_to_flat_form s rev)
            (((str.++ s1 s2)
              (alf.requires s1 sc
                (alf.define ((k (@purify (skolem_unify_split t1 s1 rev))))
                  (and
                    (= t1
                      (alf.ite rev
                        (str.++ k s1)
                        (str.++ s1 k)))
                    (not (= k (mk_emptystr (alf.typeof k))))
                    (> (str.len k) 0))
                )))))))))
)

; ProofRule::CONCAT_CPROP
(declare-rule concat_cprop ((U Type) (t (Seq U)) (tc (Seq U)) (s (Seq U)) (rev Bool))
  :premises ((= t s) (not (= (str.len tc) 0)))
  :args (rev)
  :conclusion
    (alf.match ((t1 (Seq U)) (t2 (Seq U)) (t3 (Seq U) :list))
      ($str_rev rev t)
      (((str.++ t1 t2 t3)
        (alf.requires t1 tc
          (alf.match ((s1 (Seq U)) (s2 (Seq U) :list))
            ($str_rev rev ($str_nary_intro s))  ; must do nary intro when s is just a constant
            (((str.++ s1 s2)
                (alf.define ((sc (string_to_flat_form s1 rev)))
                (alf.define ((v (alf.add 1 ($str_overlap ($tail sc) (string_to_flat_form t2 rev)))))
                  (= tc
                    (alf.ite rev
                      (alf.define ((oc ($str_suffix_w_len s1 v)))
                      (str.++ (@purify (skolem_prefix tc (- (str.len tc) (str.len oc)))) oc))
                      (alf.define ((oc (skolem_prefix s1 v)))
                      (str.++ oc (@purify (skolem_suffix_rem tc (str.len oc)))))))
                )))))))))
)

; ProofRule::CONCAT_CONFLICT, for strings only
(declare-rule concat_conflict ((s String) (t String) (rev Bool))
  :premises ((= s t))
  :args (rev)
  :conclusion
    ; strip the prefix of the equality
    (alf.match ((ss String) (ts String))
      (strip_prefix
           (string_to_flat_form s rev)
           (string_to_flat_form t rev))
      (((@pair ss ts)
          (alf.define ((cs (string_head_or_empty ss)))
          (alf.define ((ct (string_head_or_empty ts)))
            ; ensure they are disequal, return false
            (alf.requires
              (alf.ite ($str_is_empty cs)
                (string_check_length_one ct)
                (alf.ite (string_check_length_one cs)
                  (alf.ite ($str_is_empty ct)
                    true
                    (alf.ite (string_check_length_one ct)
                      (alf.not (alf.is_eq cs ct))
                      false))
                  false))
              true
              false)))))
    )
)

; ProofRule::CONCAT_CONFLICT_DEQ, for sequences only
(declare-rule concat_conflict_deq ((U Type) (s (Seq U)) (t (Seq U)) (x U) (y U) (rev Bool))
  :premises ((= s t) (not (= (seq.unit x) (seq.unit y))))
  :args (rev)
  :conclusion
    ; strip the prefix of the equality
    (alf.match ((ss (Seq U)) (ts (Seq U)))
      (strip_prefix
           (string_to_flat_form s rev)
           (string_to_flat_form t rev))
      (((@pair ss ts)
          ; take the first character from each side, should give x and y
          (alf.requires (string_head_or_empty ss) (seq.unit x)
          (alf.requires (string_head_or_empty ts) (seq.unit y)
              false)))))
)

; rule: string_decompose
; implements: ProofRule::STRING_DECOMPOSE
; premises:
; - a: An inequality between the length of some string term s and integer n.
; args:
; - b Bool: Whether we are the reverse variant of this inference.
; conclusion: >
;   A conjunction stating that s is equal to two purification skolems
;   corresponding to decomposing s at position n, and that the decomposed term
;   has length n.
(declare-rule string_decompose ((U Type) (s (Seq U)) (n Int) (b Bool))
  :premises ((>= (str.len s) n))
  :args (b)
  :conclusion (alf.ite b
                (alf.define ((kp (@purify (skolem_prefix s n)))
                      (ks (@purify (skolem_suffix_rem s n))))
                (and (= s (str.++ kp ks)) (= (str.len kp) n)))
                (alf.define ((kp (@purify ($str_prefix_wo_len s n)))
                      (ks (@purify ($str_suffix_w_len s n))))
                (and (= s (str.++ kp ks)) (= (str.len ks) n)))))

; rule: string_code_inj
; implements: ProofRule::STRING_CODE_INJ
; args:
; - t String: The first string.
; - s String: The second string.
; conclusion: >
;   A disjunction of three cases, indicating that t is not a string of length
;   one, its code point is different from that of s, or t and s are equal.
(declare-rule string_code_inj ((t String) (s String))
  :args (t s)
  :conclusion (or (= (str.to_code t) -1) (not (= (str.to_code t) (str.to_code s))) (= t s))
)

; rule: string_seq_unit_inj
; implements: ProofRule::STRING_SEQ_UNIT_INJ
; premises:
; - eq: An equality stating that the unit sequences of elements a and b are equal.
; conclusion: That a and b are equal.
(declare-rule string_seq_unit_inj ((U Type) (a U) (b U))
  :premises ((= (seq.unit a) (seq.unit b)))
  :conclusion (= a b)
)

;;-------------------- Regular expressions

; ProofRule::RE_INTER
(declare-rule re_inter ((x String) (s RegLan) (t RegLan))
  :premises ((str.in_re x s) (str.in_re x t))
  :conclusion (str.in_re x (re.inter s t))
)

; ProofRule::RE_UNFOLD_POS
(declare-rule re_unfold_pos ((t String) (r RegLan))
  :premises ((str.in_re t r))
  :conclusion
    (alf.match ((r1 RegLan) (r2 RegLan :list))
      r
      (
        ((re.* r1)
            (alf.match ((k1 String) (k2 String) (k3 String) (M Bool :list))
            (re_unfold_pos_concat t (re.++ r1 r r1))
            (((@pair (str.++ k1 k2 k3) M)
                (or 
                (= t "") 
                (str.in_re t r1)
                (and
                    (alf.cons and (= t (str.++ k1 k2 k3)) M)
                    (not (= k1 ""))
                    (not (= k3 ""))))))))
        ((re.++ r1 r2)
            (alf.match ((tk String) (M Bool :list))
            (re_unfold_pos_concat t r)
            (((@pair tk M)
                (alf.define ((teq (= t tk))) (alf.ite (alf.is_eq M true) teq (and teq M)))))))
    ))
)

; ProofRule::RE_UNFOLD_NEG_CONCAT_FIXED
(declare-rule re_unfold_neg_concat_fixed ((s String) (r RegLan) (rev Bool))
  :premises ((not (str.in_re s r)))
  :args (rev)
  :conclusion
    (alf.match ((r1 RegLan) (r2 RegLan :list))
      ($str_rev rev r)
      (
        ((re.++ r1 r2) (alf.define ((n ($str_fixed_len_re r1)))
                       (alf.ite rev
                       (or (not (str.in_re ($str_suffix_w_len s n) r1))
                           (not (str.in_re (skolem_prefix s (- (str.len s) n)) ($singleton_elim ($str_rev rev r2)))))
                       (or (not (str.in_re (skolem_prefix s n) r1))
                           (not (str.in_re (skolem_suffix_rem s n) ($singleton_elim r2)))))))
    ))
)

; ProofRule::RE_UNFOLD_NEG
(declare-rule re_unfold_neg ((t String) (r RegLan))
  :premises ((not (str.in_re t r)))
  :conclusion
    (alf.match ((r1 RegLan) (r2 RegLan :list))
      r
      (
        ((re.* r1)
          (and
            (not (= t ""))
            (forall ((@var.str_index Int))
              (or (<= @var.str_index 0)
                  (< (str.len t) @var.str_index)
                  (not (str.in_re (skolem_prefix t @var.str_index) r1))
                  (not (str.in_re (skolem_suffix_rem t @var.str_index) r))))))
        ((re.++ r1 r2)
          (forall ((@var.str_index Int))
            (or (< @var.str_index 0)
                (< (str.len t) @var.str_index)
                (not (str.in_re (skolem_prefix t @var.str_index) r1))
                (not (str.in_re (skolem_suffix_rem t @var.str_index) ($singleton_elim r2))))))
    ))
)

;;-------------------- Extended functions 

(declare-rule string_reduction ((U Type) (s U))
  :args (s)
  :conclusion (and (string_reduction_pred s) (= s (@purify s)))
)

(declare-rule string_eager_reduction ((U Type) (s U))
  :args (s)
  :conclusion (mk_string_eager_reduction s)
)

;;-------------------- Instances of THEORY_REWRITE

; rule: arith-string-pred-entail
; implements: ProofRewriteRule::ARITH_STRING_PRED_ENTAIL
; args:
; - eq Bool: The equality to prove between an integer inequality and true.
; requires: showing that n is entailed to be greater than 0 using $str_arith_entail_simple.
; conclusion: The given equality.
(declare-rule arith-string-pred-entail ((n Int))
  :args ((= (>= n 0) true))
  :requires ((($str_arith_entail_simple n) true))
  :conclusion (= (>= n 0) true)
)

; rule: arith-string-pred-entail
; implements: ProofRewriteRule::ARITH_STRING_SAFE_APPROX
; args:
; - eq Bool: The equality to prove between integer inequalities.
; requires:
; - showing that the right hand side is a valid under-approximation of the left hand side. In other words (>= n m) is entailed.
; - showing that the right hand side is entailed to be true, that is, (>= m 0) is true and thus (>= n 0) is true.
; conclusion: The given equality.
(declare-rule arith-string-pred-safe-approx ((n Int) (m Int))
  :args ((= (>= n 0) (>= m 0)))
  :requires ((($str_arith_entail_is_approx n m true) true)
             (($str_arith_entail_simple_pred (>= m 0)) true))
  :conclusion (= (>= n 0) (>= m 0))
)

(declare-rule str-in-re-eval ((s String) (r RegLan) (b Bool))
  :args ((= (str.in_re s r) b))
  :requires ((($str_eval_str_in_re s r) b))
  :conclusion (= (str.in_re s r) b)
)

; rule: str-in-re-consume
; implements: ProofRewriteRule::STR_IN_RE_CONSUME
; args:
; - eq Bool: The equality to prove whose left hand side is a regular expression membership.
; requires: >
;   Showing that consuming portions of the regular expression membership using the
;   method $str_re_consume results in b, where b is either false or the result of
;   stripping constant prefixes and suffixes from the arguments of the left hand side
;   of the given equality.
; conclusion: The given equality.
(declare-rule str-in-re-consume ((s String) (r RegLan) (b Bool))
  :args ((= (str.in_re s r) b))
  :requires ((($str_re_consume s r) b))
  :conclusion (= (str.in_re s r) b)
)

(declare-rule re-loop-elim ((l Int) (u Int) (r1 RegLan) (r2 RegLan))
  :args ((= (re.loop l u r1) r2))
  :requires (((alf.is_neg (alf.add (alf.neg l) u)) false)
             (($str_mk_re_loop_elim l (alf.add (alf.neg l) u) r1) r2))
  :conclusion (= (re.loop l u r1) r2)
)

; rule: re-inter-union-inclusion
; implements: ProofRewriteRule::RE_INTER_UNION_INCLUSION
; args:
; - eq Bool: The equality between regular expressions to prove.
; requires: >
;   showing that the right hand side of the equality is the result of
;   decomposing the left hand side into a result of a regular expression
;   inclusion and a remainder.
; conclusion: The given equality.
(declare-rule re-inter-union-inclusion ((r1 RegLan) (r2 RegLan))
  :args ((= r1 r2))
  :requires ((($str_decompose_and_check_re_inter_union_inclusion r1 r2) true))
  :conclusion (= r1 r2)
)

(declare-rule str-in-re-concat-star-char ((s1 String) (s2 String :list) (r RegLan) (b Bool))
  :args ((= (str.in_re (str.++ s1 s2) (re.* r)) b))
  :requires ((($str_fixed_len_re r) 1) 
             (($str_mk_str_in_re_concat_star_char (str.++ s1 s2) (re.* r)) b))
  :conclusion (= (str.in_re (str.++ s1 s2) (re.* r)) b)
)

(declare-rule str-in-re-sigma ((s String) (r RegLan) (b Bool))
  :args ((= (str.in_re s r) b))
  :requires ((($str_mk_str_in_re_sigma s r) b))
  :conclusion (= (str.in_re s r) b)
)

(declare-rule str-in-re-sigma-star ((s String) (r RegLan) (b Bool))
  :args ((= (str.in_re s (re.* r)) b))
  :requires ((($str_mk_str_in_re_sigma_star s r) b))
  :conclusion (= (str.in_re s (re.* r)) b)
)
