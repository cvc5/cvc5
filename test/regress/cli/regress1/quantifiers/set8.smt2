; COMMAND-LINE: --ieval=use-learn
; EXPECT: unsat
(set-logic AUFLIA)
(set-info :source | Set theory. |)
(set-info :smt-lib-version 2.6)
(set-info :category "crafted")
(set-info :status unsat)
(declare-sort Set 0)
(declare-sort Elem 0)
(declare-fun set.member (Elem Set) Bool)
(declare-fun set.subset (Set Set) Bool)
(assert (forall ((?x Elem) (?s1 Set) (?s2 Set)) (=> (and (set.member ?x ?s1) (set.subset ?s1 ?s2)) (set.member ?x ?s2))))
(assert (forall ((?s1 Set) (?s2 Set)) (=> (not (set.subset ?s1 ?s2)) (exists ((?x Elem)) (and (set.member ?x ?s1) (not (set.member ?x ?s2)))))))
(assert (forall ((?s1 Set) (?s2 Set)) (=> (forall ((?x Elem)) (=> (set.member ?x ?s1) (set.member ?x ?s2))) (set.subset ?s1 ?s2))))
(declare-fun seteq (Set Set) Bool)
(assert (forall ((?s1 Set) (?s2 Set)) (= (seteq ?s1 ?s2) (= ?s1 ?s2))))
(assert (forall ((?s1 Set) (?s2 Set)) (= (seteq ?s1 ?s2) (and (set.subset ?s1 ?s2) (set.subset ?s2 ?s1)))))
(declare-fun union (Set Set) Set)
(assert (forall ((?x Elem) (?s1 Set) (?s2 Set)) (= (set.member ?x (union ?s1 ?s2)) (or (set.member ?x ?s1) (set.member ?x ?s2)))))
(declare-fun set.inter (Set Set) Set)
(assert (forall ((?x Elem) (?s1 Set) (?s2 Set)) (= (set.member ?x (set.inter ?s1 ?s2)) (and (set.member ?x ?s1) (set.member ?x ?s2)))))
(declare-fun difference (Set Set) Set)
(assert (forall ((?x Elem) (?s1 Set) (?s2 Set)) (= (set.member ?x (difference ?s1 ?s2)) (and (set.member ?x ?s1) (not (set.member ?x ?s2))))))
(declare-fun a () Set)
(declare-fun b () Set)
(assert (not (seteq (set.inter a b) (set.inter b a))))
(check-sat)
(exit)
