; REQUIRES: normaliz
; DISABLE-TESTER: lfsc
(set-logic ALL)
(set-option :incremental true)
;
; Points that satisfy y >= 5-x and y >= x - 5 are shown as dots below. 
; Such points can be generated by two rays (0,1), (1,1) starting from any  
; offset in {(0,5),(1,4),(2,3),(3,2),(4,1),(5,0)}. 
; For example, (1,1) can not be generated this way, whereas (4,4) can.
;
;      y
;    6 +   .   .   .   .   .   .
;      |
;    5 ↑↗  .   .   .   .   .   .
;      |
;    4 +   ↑↗  .   .   .   .   .
;      |
;    3 +   +   ↑↗  .   .   .   .
;      |
;    2 +   +   +   ↑↗  .   .   .
;      |
;    1 +   +   +   +   ↑↗  +   .
;      |
;      +---+---+---+---+---+↑↗-+ x
;      0   1   2   3   4   5   6

(set-info :status unsat)
(push 1)
(assert 
 (int.star-contains 
  ((x Int) (y Int)) 
  (and     
    (>= (+ (+ x y) (- 5)) 0) ; y >= 5-x
    (>= (+ (+ (* (- 1) x) y) 5) 0) ; y >= x - 5
  )
  (tuple 1 1)))
(check-sat)
(pop 1)

(set-info :status sat)
(push 1)
(assert 
 (int.star-contains 
  ((x Int) (y Int)) 
  (and     
    (>= (+ (+ x y) (- 5)) 0) ; y >= 5-x
    (>= (+ (+ (* (- 1) x) y) 5) 0) ; y >= x - 5
  )
  (tuple 4 4)))
(check-sat)
(pop 1)
