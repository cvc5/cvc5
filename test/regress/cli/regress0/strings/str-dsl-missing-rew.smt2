; EXPECT: unsat
(set-logic ALL)
(declare-fun a () String)
(declare-fun b () String)
(declare-fun c () String)
(declare-fun d () Int)
(declare-fun e () Int)
(assert (or
(not (= (str.len (str.replace_all a "A" "B")) (str.len a)))
(not (= (str.replace_all a (str.++ a "A") "B") a))
(not (= (str.contains "" a) (= "" a)))
(not (= (str.to_upper (str.++ a b)) (str.++ (str.to_upper a) (str.to_upper b))))
(not (= (str.to_upper (str.from_int d)) (str.from_int d)))
(not (str.contains a (str.substr a d e)))
(not (str.contains (str.++ b a c) (str.substr a d e)))
(str.<= "B" (str.++ "A" a))
(not (= (str.replace (str.++ "AB" c) "B" c) (str.++ (str.replace "AB" "B" c) c)))
(not (= (str.indexof (str.++ "ABC" a) "B" 1) 1))
(not (= (str.substr (str.++ a b c) (str.len a) 5) (str.substr (str.++ b c) 0 5)))
))
(check-sat)
